apply plugin: 'com.android.application'

android {
    compileSdkVersion 26

    defaultConfig {
        applicationId "com.gsc.testapp"
        minSdkVersion 21
        targetSdkVersion 26
        versionCode versionCodeProperty as Integer
        versionName versionNameProperty
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            //get signing properties file from command line
            if (project.hasProperty('signPropsFile')) {
                def propsFilePath = project.getProperty('signPropsFile')

                def propsFile = new File(propsFilePath)

                if (propsFile.canRead()) {
                    def props = new Properties()
                    props.load(new FileInputStream(propsFile))

                    storeFile = file(props['storeFile'])
                    storePassword = props['storePassword']
                    keyAlias = props['keyAlias']
                    keyPassword = props['keyPassword']
                }
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            buildConfigField("String", "DOMAIN", '"test.elimu.ai"')
            buildConfigField("String", "BASE_URL", '"http://test.elimu.ai"')
            buildConfigField("String", "REST_URL", '"http://test.elimu.ai/rest/v1"')
            buildConfigField("String", "APPSTORE_SECRET", '"rafiki"')
        }

        release {
            buildConfigField("String", "DOMAIN", '"elimu.ai"')
            buildConfigField("String", "BASE_URL", '"http://elimu.ai"')
            buildConfigField("String", "REST_URL", '"http://elimu.ai/rest/v1"')
            buildConfigField("String", "APPSTORE_SECRET", '"57million"')
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation "com.android.support:appcompat-v7:26.0.1"
    testImplementation 'junit:junit:4.12'
}
